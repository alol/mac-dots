# .zshrc
# vim:syntax=sh

# MACDOTS and ZSH are set in zprofile.symlink

# Temporary utility function
function _source_if_exists {
  local file ;
  for file ; do
    [ -f "${file}" ] && source "${file}" ;
  done
}

# all of our zsh files
typeset -U config_files ;
config_files=($MACDOTS/**/*.zsh) ;

# Before anything else
_source_if_exists "${HOME}/.before-mac-dots" ;

# load the path files
for file in ${(M)config_files:#*/path.zsh} ; do
  source $file ;
done

# load everything but the aliases, path and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/aliases.zsh} ; do
  source $file ;
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit ;
compinit ;

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
  source $file ;
done

# Load RVM into shell session...
_source_if_exists "${HOME}/.rvm/scripts/rvm" ;

# Load OMZ
source $ZSH/oh-my-zsh.sh

# After everything else
_source_if_exists "${HOME}/.after-mac-dots" ;

# finally, load the aliases files - this happens last
# to allow our own aliases to take precedence
for file in ${(M)config_files:#*/aliases.zsh} ; do
  source $file ;
done

PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

# load any user-defined functions here
if [[ -n "$MACDOTS_FUNCTIONS_PATH" && -d "$MACDOTS_FUNCTIONS_PATH" ]] ; then
	custom_files=(`find $MACDOTS_FUNCTIONS_PATH -name "*.macdots_function"`) ;
	for file in ${custom_files} ; do
	  source $file ;
	done
fi

# finally, clean up
unset config_files ;
unset _source_if_exists ;
